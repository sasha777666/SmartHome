
//Универсальные функции (

Функция СформироватьСтруктуруДанныхСправочника(Ссылка) Экспорт 
	
	СтруктураДляПередачи = Новый Структура();
	
	СтруктураДляПередачиРеквизиты = Новый Структура();
	СтруктураДляПередачиСтандартныеРеквизиты = Новый Структура();
	СтруктураДляПередачиТаблицы = Новый Структура();
		
	Для Каждого Элемент Из Ссылка.Метаданные().Реквизиты Цикл
		СтруктураДляПередачиРеквизиты.Вставить(Элемент.Имя, Ссылка[Элемент.Имя]);
	КонецЦикла;
	
	Для Каждого Элемент Из Ссылка.Метаданные().СтандартныеРеквизиты Цикл 
		СтруктураДляПередачиСтандартныеРеквизиты.Вставить(Элемент.Имя, Ссылка[Элемент.Имя]);
	КонецЦикла;
		                 
	Для Каждого Элемент Из Ссылка.Метаданные().ТабличныеЧасти Цикл
		 СтруктураДляПередачиТаблицы.Вставить(Элемент.Имя, Ссылка[Элемент.Имя].Выгрузить()); 		
	КонецЦикла;
	
	СтруктураДляПередачи.Вставить("Тип", "Справочники." + Ссылка.Метаданные().Имя); 
	СтруктураДляПередачи.Вставить("Реквизиты", СтруктураДляПередачиРеквизиты);
	СтруктураДляПередачи.Вставить("СтандартныеРеквизиты", СтруктураДляПередачиСтандартныеРеквизиты);
	СтруктураДляПередачи.Вставить("ТабличныеЧасти", СтруктураДляПередачиТаблицы);
	
	Возврат СтруктураДляПередачи;
	
КонецФункции

// ) Универсальные функции 


//Интеграция с другими базами этой конфигурации  (

Процедура Обмен_ОтправитьДанныеСправочникаВЦентральнуюБазу(Ссылка) Экспорт
	
	Обмен_ОтправитьДанныеСправочника(Ссылка, Константы.АдресЦентральнойБазы.Получить());
	
КонецПроцедуры	
	
	
Процедура Обмен_ОтправитьДанныеСправочника(Ссылка, АдресРесурса) Экспорт
		
	Обмен_ОтправитьДанныеСправочникаПодготовитьДанные(Ссылка, АдресРесурса);
	Если Ссылка.ЭтоГруппа Тогда 
		Выборка = Справочники[Ссылка.Метаданные().Имя].ВыбратьИерархически(Ссылка);
		Пока Выборка.Следующий() Цикл
			Обмен_ОтправитьДанныеСправочникаПодготовитьДанные(Выборка.Ссылка, АдресРесурса);			
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры
Процедура Обмен_ОтправитьДанныеСправочникаПодготовитьДанные(Ссылка, АдресРесурса)
	
	СтруктураСДанными = СформироватьСтруктуруДанныхСправочника(Ссылка);
	СтрокаСДанными = ЗначениеВСтрокуВнутр(СтруктураСДанными);
	
	ОтправитьДвоичныеДанныеПостЗапросом(СтрокаСДанными, АдресРесурса);
	 
КонецПроцедуры
Процедура ОтправитьДвоичныеДанныеПостЗапросом(СтрокаСДанными, АдресРесурса)
	
	Соединение = Новый HTTPСоединение(АдресРесурса);
	ЗапросСервера = Новый HTTPЗапрос(Константы.ИмяЦентральнойБазы.Получить() + "/hs/Data_Exchange_Element");
	ЗапросСервера.УстановитьТелоИзСтроки(СтрокаСДанными);
	ОтветСервера = Соединение.ВызватьHTTPМетод("POST", ЗапросСервера);
	ТелоОтвета = ОтветСервера.ПолучитьТелоКакСтроку();
	КодОтвета = ОтветСервера.КодСостояния;
	
КонецПроцедуры

Процедура ПринятьДанныеИзСтруктуры(Структура) Экспорт
	
	Если Лев(Структура.Тип, 11) = "Справочники" Тогда 
		ПринятьДанныеСправочника(Структура);
	Иначе
		
	КонецЕсли;
		
КонецПроцедуры
Процедура ПринятьДанныеСправочника(Структура)

	Если Структура.СтандартныеРеквизиты.ЭтоГруппа Тогда 
		СозданныйОбъект = Справочники[Сред(Структура.Тип, 13)].СоздатьГруппу();		
	Иначе
		СозданныйОбъект = Справочники[Сред(Структура.Тип, 13)].СоздатьЭлемент();
	КонецЕсли;
	
	Для Каждого Элемент Из Структура.Реквизиты Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			СозданныйОбъект[Элемент.Ключ] = Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из Структура.СтандартныеРеквизиты Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда 
			Если Лев(Строка(Элемент.Значение),18) <> "<Объект не найден>" 
			И Элемент.Ключ <> "ИмяПредопределенныхДанных" И Элемент.Ключ <> "Предопределенный" 
			И Элемент.Ключ <> "Ссылка" И Элемент.Ключ <> "ЭтоГруппа" И Элемент.Ключ <> "Код" Тогда		
				СозданныйОбъект[Элемент.Ключ] = Элемент.Значение;			
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из Структура.ТабличныеЧасти Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			СозданныйОбъект[Элемент.Ключ].Загрузить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;

	СозданныйОбъект.Записать();	
	
КонецПроцедуры

// ) Интеграция с другими базами этой конфигурации 
