

Процедура ПроцессДляСервераЗаписьФото(Процесс = "") Экспорт // Записывается в процессы НазваниеПроцедуры
	
	ИнициализацияДляФото();	
	
	ТекДанныеКомандыТранслировать = РегистрыСведений.ТекущиеДанныеОборудования.Получить(Новый Структура("Оборудование, ОбъектДанных",Оборудование, "Транслировать"));
	Если ТекДанныеКомандыТранслировать.Дата + 3 >= ТекущаяДата() И ТекДанныеКомандыТранслировать.Данные = 1 Тогда // включена трансляция и фото уже получено
		ДвоичныеДанныеФото = РегистрыСведений.ТекущиеДанныеОборудования.Получить(Новый Структура("Оборудование, ОбъектДанных",Оборудование, "Трансляция")).Хранилище.Получить(); // получим фото из регистра	
	Иначе
		ДвоичныеДанныеФото = ПолучитьФотографиюВнутренняя(); // получим фото с камеры	
	КонецЕсли;
	
	СохранитьСнимок(ДвоичныеДанныеФото);	
	
КонецПроцедуры

Функция ПолучитьДвоичныеДанныеФотографии() Экспорт
	
	ИнициализацияДляФото();	
	ДвоичныеДанныеФото = ПолучитьФотографиюВнутренняя();	
	
	Возврат ДвоичныеДанныеФото;
	
КонецФункции

////////////////////////////
Процедура ИнициализацияДляФото() 
	
	АдресКамеры = Оборудование.КонтроллерCOMNET.АдресПорт;

	Ответ = ЗаполнитьНастройкиДляФото();
	Если ЗначениеЗаполнено(Ответ) Тогда 
		Сообщения = Сообщения + Символы.ПС + ТекущаяДата() + " " + Ответ;
	КонецЕсли;

	
КонецПроцедуры

Функция  ЗаполнитьНастройкиДляФото() 
	
	СообщениеСНенайденнымиНастройками = "";
	
	// Настройки в реквизитах
	СтрокаСТабСНастройкамиРеквизитов = Оборудование.Настройки.Найти("Реквизиты",  "Наименование");
	Если СтрокаСТабСНастройкамиРеквизитов = Неопределено Тогда 
		СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не заполнена настройка для реквизитов (В справочнике оборудование); ";
	Иначе
		ТабСНастройкамиРеквизитов = СтрокаСТабСНастройкамиРеквизитов.Настройка.ТаблицаНастроек;
				
		СтрокаСПараметром = ТабСНастройкамиРеквизитов.Найти("Логин", "Параметр");
		Если СтрокаСПараметром = Неопределено Тогда 
			СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не указан параметр ""Логин""; ";
		Иначе
			Логин = СтрокаСПараметром.Значение;
		КонецЕсли;

		СтрокаСПараметром = ТабСНастройкамиРеквизитов.Найти("Пароль", "Параметр");
		Если СтрокаСПараметром = Неопределено Тогда 
			СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не указан параметр ""Пароль""; ";
		Иначе
			Пароль = СтрокаСПараметром.Значение;
		КонецЕсли;

		СтрокаСПараметром = ТабСНастройкамиРеквизитов.Найти("Порт", "Параметр");
		Если СтрокаСПараметром = Неопределено Тогда 
			СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не указан параметр ""Порт""; ";
		Иначе
			Порт = СтрокаСПараметром.Значение;
		КонецЕсли;

		
		СтрокаСПараметром = ТабСНастройкамиРеквизитов.Найти("ПутьДляХраненияФотографийВРабочейПапке", "Параметр");
		Если СтрокаСПараметром = Неопределено Тогда 
			СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не указан параметр ""ПутьДляХраненияФотографийВРабочейПапке""; ";
		Иначе
			ПутьДляХраненияФотографийВРабочейПапке = СтрокаСПараметром.Значение;
		КонецЕсли;

		
	КонецЕсли;
		
	Возврат СообщениеСНенайденнымиНастройками;
	

КонецФункции

Функция ПолучитьФотографиюВнутренняя() 
	
	Соединение = Новый HTTPСоединение(АдресКамеры, Порт , Логин, Пароль);
	
	Запрос = Новый HTTPЗапрос("/cgi-bin/snapshot.cgi?");
	Ответ1 =  Соединение.ОтправитьДляОбработки(Запрос);
	Authenticate = Ответ1.Заголовки.Получить("WWW-Authenticate");
	ЧастьAuthenticate = Прав(Authenticate,СтрДлина(Authenticate) - Найти(Authenticate,"nonce=") -6);
	Нонс = Лев(ЧастьAuthenticate,Найти(ЧастьAuthenticate,"""")-1);
	
	Авро = СтрЗаменить(Authenticate,"Digest realm=","");
	Realm = СтрЗаменить(Лев(Авро,Найти(Авро,""",")),"""","");
		
	URI = "/cgi-bin/snapshot.cgi?";
		    
	A1 =  MD5(Логин + ":" + Realm + ":" + Пароль);
	А2 = MD5("GET:"+URI);
	
	ОтветМД5 = НРег(MD5(НРег(A1)+":"+Нонс+":00000001:4d705c8aa158ff60:auth:"+НРег(А2)));	
	Запрос.Заголовки.Вставить("Authorization","Digest username="+Логин+", realm="+Realm+", nonce="""+Нонс+""", uri="+URI+", algorithm=MD5, response="""+ОтветМД5+""", qop=auth, nc=00000001, cnonce=""4d705c8aa158ff60""");	
	
	Ответ2 =  Соединение.Получить(Запрос);
		
	ДвоичныеДанные = Ответ2.ПолучитьТелоКакДвоичныеДанные();
	
	Возврат ДвоичныеДанные; 
	
КонецФункции


Процедура СохранитьСнимок(ДвоичныеДанныеФото)
	
	ДатаСнимка = ТекущаяДата();
	ГодСнимка = Формат(ДатаСнимка, "ДФ=yyyy");
	ГодМесяцДеньСнимка = Формат(ДатаСнимка, "ДФ=yyyy-MM-dd");
	ГодМесяцДеньЧасМинутаСекундаСнимка = Формат(ДатаСнимка, "ДФ=yyyy-MM-dd-HH-mm-ss");
	РабПапка = Константы.КаталогФайловРабочий.Получить();
	
	ПутьДляКаталогаФото = "" + РабПапка + ПутьДляХраненияФотографийВРабочейПапке + "\" + ГодМесяцДеньСнимка;
	ПутьДляФайлаФотоБезРасширения = "" + РабПапка + ПутьДляХраненияФотографийВРабочейПапке + "\" + ГодМесяцДеньСнимка + "\" + ГодМесяцДеньЧасМинутаСекундаСнимка;
	ПутьДляФайлаФотоСРасширением = ПутьДляФайлаФотоБезРасширения + ".jpg";
	
	СоздатьКаталог(ПутьДляКаталогаФото); 	
	ФайлФото = Новый Файл(ПутьДляФайлаФотоСРасширением);
	н = 1;
	Пока ФайлФото.Существует() Цикл 
		ПутьДляФайлаФотоСРасширением = ПутьДляФайлаФотоБезРасширения + "_" + н + ".jpg"; 
		ФайлФото = Новый Файл(ПутьДляФайлаФотоСРасширением);
		н = н + 1;
	КонецЦикла;;
	
	Фотография = Новый Картинка(ДвоичныеДанныеФото);
	Фотография.Записать(ПутьДляФайлаФотоСРасширением);	
	
КонецПроцедуры


///////////////////////////////////////////

&НаСервере
Функция MD5(СтрокаДляХеширования)
  
  Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
  Хеш.Добавить(СтрокаДляХеширования);
  Возврат СтрЗаменить(Строка(Хеш.ХешСумма), " ", "");
    
КонецФункции


