

Функция СкомпоноватьСообщенияГруппыЛинейно(ТелеграмГруппаСообщений, НазваниеГруппы = "") Экспорт
	
	
	РабочийКаталог = Константы.КаталогФайловРабочий.Получить();
	ПолныйПутьКПапке = РабочийКаталог + "\Telegram\assembled\" + Формат(ТелеграмГруппаСообщений.Дата, "ДФ=yyyy-MM-dd") + "_" + Формат(ТелеграмГруппаСообщений.Номер, "ЧДЦ=0; ЧГ=0") + ?(НазваниеГруппы = "", "","_" + НазваниеГруппы); 
	СоздатьКаталог(ПолныйПутьКПапке);
		
	ЗапросСообщенийГруппы = Новый ЗАпрос();
	ЗапросСообщенийГруппы.Текст = "ВЫБРАТЬ
	                              |	ТелеграмСообщенияГруппыСообщенийОбороты.Регистратор КАК Регистратор,
	                              |	ТелеграмВложенияСообщенийВходящих.ПутьФайлаВТелеграме КАК ПутьФайлаВТелеграме,
	                              |	ТелеграмВложенияСообщенийВходящих.Тип КАК Тип,
	                              |	ТелеграмВложенияСообщенийВходящих.Описание КАК Описание,
	                              |	ТелеграмВложенияСообщенийВходящих.Размер КАК Размер,
	                              |	ТелеграмВложенияСообщенийВходящих.ПутьКФайлу КАК ПутьКФайлу,
	                              |	ТелеграмВложенияСообщенийВходящих.ДатаЗагрузки КАК ДатаЗагрузки,
	                              |	ТелеграмСообщенияГруппыСообщенийОбороты.Регистратор.Текст КАК РегистраторТекст,
	                              |	ТелеграмСообщенияГруппыСообщенийОбороты.Регистратор.Дата КАК РегистраторДата
	                              |ИЗ
	                              |	РегистрНакопления.ТелеграмСообщенияГруппыСообщений.Обороты(, , Регистратор, ТелеграмГруппаСообщений = &ТелеграмГруппаСообщений) КАК ТелеграмСообщенияГруппыСообщенийОбороты
	                              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТелеграмВложенияСообщенийВходящих КАК ТелеграмВложенияСообщенийВходящих
	                              |		ПО ТелеграмСообщенияГруппыСообщенийОбороты.Регистратор = ТелеграмВложенияСообщенийВходящих.Сообщение
	                              |
	                              |УПОРЯДОЧИТЬ ПО
	                              |	ТелеграмСообщенияГруппыСообщенийОбороты.Период,
	                              |	ДатаЗагрузки
	                              |ИТОГИ ПО
	                              |	Регистратор";
	ЗапросСообщенийГруппы.УстановитьПараметр("ТелеграмГруппаСообщений", ТелеграмГруппаСообщений);
	ВыборкаСообщения = ЗапросСообщенийГруппы.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	МенеджерПрогресса = РегистрыСведений.ПрогрессВыполненияСерверныхПроцедур.СоздатьМенеджерЗаписи();
	МенеджерПрогресса.ПолноеИмяОбработчика = "Обработки.ТелеграмКомпоновкаСообщенийГруппы.СкомпоноватьСообщенияГруппыЛинейно";	
	МенеджерПрогресса.Прогресс = 0;
	МенеджерПрогресса.ДатаЗаписи = ТекущаяДата();
	МенеджерПрогресса.Записать(Истина);
	
	ШагПрогресса = 100 / ВыборкаСообщения.Количество();
	ТекущийПроцегресс = 0;
	НомерФайла = 0;
	СообщенияТекст = "Группа номер: " + ТелеграмГруппаСообщений.Номер + " создана: " + ТелеграмГруппаСообщений.Дата + " пользователем: " + ТелеграмГруппаСообщений.ТелеграмКонтакт 
	+ Символы.ПС + ?(НазваниеГруппы = "", "", "Название группы: " + НазваниеГруппы + Символы.ПС)+ Символы.ПС;
	Пока ВыборкаСообщения.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаСообщения.РегистраторТекст) Тогда 
			СообщенияТекст = СообщенияТекст + Строка(ВыборкаСообщения.РегистраторДата) + " - " + ВыборкаСообщения.РегистраторТекст + Символы.ПС;  
		КонецЕсли;
		ВыборкаВложения = ВыборкаСообщения.Выбрать();
		
		Пока ВыборкаВложения.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаВложения.ПутьКФайлу) Тогда
				НомерФайла = НомерФайла + 1;
				РасширениеФайла = Сред(ВыборкаВложения.ПутьКФайлу,СтрНайти(ВыборкаВложения.ПутьКФайлу, ".", НаправлениеПоиска.СКонца)+1); 
				ПолныйПутьНовогоФайла = ПолныйПутьКПапке + "\" + НомерФайла + "_" + Формат(ВыборкаВложения.РегистраторДата, "ДФ=yyyy-MM-dd-ЧЧ-мм-сс") + ?(ВыборкаВложения.Описание="", "", "_" + ВыборкаВложения.Описание) + "." + РасширениеФайла; 		
				КопироватьФайл(ВыборкаВложения.ПутьКФайлу,ПолныйПутьНовогоФайла);		
			КонецЕсли;
		КонецЦикла;		
			
		ТекущийПроцегресс = ТекущийПроцегресс + ШагПрогресса;	
		МенеджерПрогресса.Прогресс = Окр(ТекущийПроцегресс, 0, РежимОкругления.Окр15как10);
		МенеджерПрогресса.ДатаЗаписи = ТекущаяДата();
		МенеджерПрогресса.Записать(Истина);
	КонецЦикла;
	
	ПолныйПутьКТекстовомуФайлу = ПолныйПутьКПапке + "\0_Группа номер " + Строка(ТелеграмГруппаСообщений.Номер) + ".txt";   
	ЗаписьТекста = Новый ЗаписьТекста();
	ЗаписьТекста.Открыть(ПолныйПутьКТекстовомуФайлу);
	ЗаписьТекста.Записать(СообщенияТекст);
	ЗаписьТекста.Закрыть();
	
	//Геоданные 
	ЗапросГеоданных = Новый Запрос();
	ЗапросГеоданных.Текст = "ВЫБРАТЬ
	                        |	ТелеграмГеоданныеКонтакта.Период КАК Период,
	                        |	ТелеграмГеоданныеКонтакта.latitude КАК latitude,
	                        |	ТелеграмГеоданныеКонтакта.longitude КАК longitude
	                        |ИЗ
	                        |	(ВЫБРАТЬ
	                        |		ПериодыОткрытияГруппы.Период КАК ПериодОткрытия,
	                        |		МИНИМУМ(ПериодыЗакрытияГруппы.Период) КАК ПериодЗакрытия
	                        |	ИЗ
	                        |		(ВЫБРАТЬ
	                        |			ТелеграмГруппыСообщенийСостояние.Период КАК Период
	                        |		ИЗ
	                        |			РегистрСведений.ТелеграмГруппыСообщенийСостояние КАК ТелеграмГруппыСообщенийСостояние
	                        |		ГДЕ
	                        |			ТелеграмГруппыСообщенийСостояние.Открыта
	                        |			И ТелеграмГруппыСообщенийСостояние.ТелеграмГруппаСообщений = &ТелеграмГруппаСообщений) КАК ПериодыОткрытияГруппы
	                        |			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                        |				ТелеграмГруппыСообщенийСостояние.Период КАК Период
	                        |			ИЗ
	                        |				РегистрСведений.ТелеграмГруппыСообщенийСостояние КАК ТелеграмГруппыСообщенийСостояние
	                        |			ГДЕ
	                        |				НЕ ТелеграмГруппыСообщенийСостояние.Открыта
	                        |				И ТелеграмГруппыСообщенийСостояние.ТелеграмГруппаСообщений = &ТелеграмГруппаСообщений) КАК ПериодыЗакрытияГруппы
	                        |			ПО ПериодыОткрытияГруппы.Период < ПериодыЗакрытияГруппы.Период
	                        |	
	                        |	СГРУППИРОВАТЬ ПО
	                        |		ПериодыОткрытияГруппы.Период) КАК ВложенныйЗапрос
	                        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТелеграмГеоданныеКонтакта КАК ТелеграмГеоданныеКонтакта
	                        |		ПО ВложенныйЗапрос.ПериодОткрытия <= ТелеграмГеоданныеКонтакта.Период
	                        |			И ВложенныйЗапрос.ПериодЗакрытия >= ТелеграмГеоданныеКонтакта.Период
	                        |ГДЕ
	                        |	ТелеграмГеоданныеКонтакта.ТелеграмКонтакт = &ТелеграмКонтакт
	                        |
	                        |УПОРЯДОЧИТЬ ПО
	                        |	Период";
	ЗапросГеоданных.УстановитьПараметр("ТелеграмГруппаСообщений", ТелеграмГруппаСообщений);
	ЗапросГеоданных.УстановитьПараметр("ТелеграмКонтакт", ТелеграмГруппаСообщений.ТелеграмКонтакт);
	ВыборкаГеоданных = ЗапросГеоданных.Выполнить().Выбрать();
	
	ПолныйПутьКТекстовомуФайлуГеоданных = ПолныйПутьКПапке + "\0_Геоданные.txt";
	ЗаписьТекста.Открыть(ПолныйПутьКТекстовомуФайлуГеоданных);
	Пока ВыборкаГеоданных.Следующий() Цикл
		ЗаписьТекста.ЗаписатьСтроку(Строка(ВыборкаГеоданных.Период) + " - latitude: " + ВыборкаГеоданных.latitude + " longitude: " + ВыборкаГеоданных.longitude);  	
	КонецЦикла;
	ЗаписьТекста.Закрыть();
	
	МенеджерПрогресса.Прогресс = 100;
	МенеджерПрогресса.ДатаЗаписи = ТекущаяДата();
	МенеджерПрогресса.Записать(Истина);

	Возврат ПолныйПутьКПапке;
	
КонецФункции

