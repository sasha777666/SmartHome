
Процедура ПроцедураДляВызова(СсылкаДокВхСообщение) Экспорт
	
	ПозицияАдресаБота = СтрНайти(СсылкаДокВхСообщение.Текст, Справочники.ТелеграмПараметры.АдресБота.Значение);
	Если ПозицияАдресаБота > 0 Тогда
		ТекстСообщения = Лев(СсылкаДокВхСообщение.Текст, ПозицияАдресаБота-1);
	Иначе
		ТекстСообщения = СсылкаДокВхСообщение.Текст;
	КонецЕсли;

	МенеджерЗаписи = РегистрыСведений.ТелеграмОбработкаСообщенийИИАктивные.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ТелеграмКонтакт = СсылкаДокВхСообщение.ТелеграмКонтакт;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.РасположениеПроцедуры <> "Обработки.ТелеграмИИОбработкаСообщенийГруппы" или МенеджерЗаписи.НазваниеПроцедуры <> "ПроцедураДляВызова"  Тогда
		//Это первая итерация
		МенеджерЗаписи.ТелеграмКонтакт = СсылкаДокВхСообщение.ТелеграмКонтакт;
		МенеджерЗаписи.РасположениеПроцедуры = "Обработки.ТелеграмИИОбработкаСообщенийГруппы";
		МенеджерЗаписи.НазваниеПроцедуры = "ПроцедураДляВызова";
		ТекущиеДанные = Новый Структура();
		ТекущиеДанные.Вставить("КолВыполненныхИтераций", 1);
		ТекущиеДанные.Вставить("ОжидаемыйТипОтветаПользователя", "НомерГруппы");
		ТекущиеДанные.Вставить("НомерВыбраннойТелеграмГруппыСообщений", 0);
		ТекущиеДанные.Вставить("НазваниеПапки", "");

		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 10
		               |	ТелеграмГруппыСообщенийСостояниеСрезПоследних.Период КАК Период,
		               |	ТелеграмГруппыСообщенийСостояниеСрезПоследних.ТелеграмГруппаСообщений КАК ТелеграмГруппаСообщений
		               |ИЗ
		               |	РегистрСведений.ТелеграмГруппыСообщенийСостояние.СрезПоследних КАК ТелеграмГруппыСообщенийСостояниеСрезПоследних
		               |ГДЕ
		               |	НЕ ТелеграмГруппыСообщенийСостояниеСрезПоследних.Открыта
		               |	И ТелеграмГруппыСообщенийСостояниеСрезПоследних.ТелеграмГруппаСообщений.ТелеграмКонтакт = &ТелеграмКонтакт
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТелеграмГруппыСообщенийСостояниеСрезПоследних.ТелеграмГруппаСообщений.Дата";
		Запрос.УстановитьПараметр("ТелеграмКонтакт", СсылкаДокВхСообщение.ТелеграмКонтакт);
		Выборка = Запрос.Выполнить().Выбрать();
				
		Текст = "Выберите группу:";
		
		Пока Выборка.Следующий() Цикл
			Текст = Текст + Символы.ПС + "/" + Формат(Выборка.ТелеграмГруппаСообщений.Номер, "ЧГ=0") + " " + Выборка.ТелеграмГруппаСообщений.Дата; 			
		КонецЦикла;
		
		 
		МенеджерЗаписи.ТекущиеДанные = Новый ХранилищеЗначения(ТекущиеДанные);
		МенеджерЗаписи.ДатаЗаписи = ТекущаяДата();
		МенеджерЗаписи.Записать(Истина);		
		
		Сообщение = Новый Структура();
		Сообщение.Вставить("ТелеграмКонтакт", СсылкаДокВхСообщение.ТелеграмКонтакт);
		Сообщение.Вставить("Текст", Текст);
		ТелеграмМодуль.СоздатьИОтправитьДокСообщениеИсходящие(Сообщение);
		
	Иначе
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ТекущиеДанные = МенеджерЗаписи.ТекущиеДанные.Получить();
			ТекущиеДанные.КолВыполненныхИтераций = ТекущиеДанные.КолВыполненныхИтераций + 1;
			
		    Если ТекущиеДанные.ОжидаемыйТипОтветаПользователя = "НомерГруппы" Тогда //Пользователь должен прислать номер телеграм группы сообщений
				ТекущиеДанные.НомерВыбраннойТелеграмГруппыСообщений = Число(Сред(ТекстСообщения, 2));
				
				ТекущиеДанные.Вставить("ОжидаемыйТипОтветаПользователя", "НазваниеПапки");
				
				Сообщение = Новый Структура();
				Сообщение.Вставить("ТелеграмКонтакт", СсылкаДокВхСообщение.ТелеграмКонтакт);
				Сообщение.Вставить("Текст", "Введите название каталога с компоновкой");
				ТелеграмМодуль.СоздатьИОтправитьДокСообщениеИсходящие(Сообщение);
			ИначеЕсли ТекущиеДанные.ОжидаемыйТипОтветаПользователя = "НазваниеПапки" Тогда
				ТекущиеДанные.НазваниеПапки = ТекстСообщения;
				
				Текст = "Куда отправить скомпонованные данные:";
				Для Каждого Строка Из Справочники.Настройки.СписокКаталоговКомпоновки.ТаблицаНастроек Цикл
					Текст = Текст + Символы.ПС + "/" + Строка.Параметр;	
				КонецЦикла;
				ТекущиеДанные.Вставить("ОжидаемыйТипОтветаПользователя", "МестоНазначения");
				
				Сообщение = Новый Структура();
				Сообщение.Вставить("ТелеграмКонтакт", СсылкаДокВхСообщение.ТелеграмКонтакт);
				Сообщение.Вставить("Текст", Текст);
				ТелеграмМодуль.СоздатьИОтправитьДокСообщениеИсходящие(Сообщение);
			ИначеЕсли ТекущиеДанные.ОжидаемыйТипОтветаПользователя = "МестоНазначения" Тогда
				СтрокаНастройки = Справочники.Настройки.СписокКаталоговКомпоновки.ТаблицаНастроек.Найти(Сред(ТекстСообщения, 2));
				
				Если ЗначениеЗаполнено(СтрокаНастройки) Тогда // Выбран каталог из настроек
					//Отправим сообщение что начали
					Сообщение = Новый Структура();
					Сообщение.Вставить("ТелеграмКонтакт", СсылкаДокВхСообщение.ТелеграмКонтакт);
					Сообщение.Вставить("Текст", "Операция начата");
					ТелеграмМодуль.СоздатьИОтправитьДокСообщениеИсходящие(Сообщение);
					
					ПолныйПутьКЦелевомуКаталогу = СтрокаНастройки.Значение;		
					ОбрКомпоновки = Обработки.ТелеграмКомпоновкаСообщенийГруппы.Создать();
					ПолныйПутьКПапкеИсточника = ОбрКомпоновки.СкомпоноватьСообщенияГруппыЛинейно(Документы.ТелеграмГруппыСообщений.НайтиПоНомеру(ТекущиеДанные.НомерВыбраннойТелеграмГруппыСообщений), ТекущиеДанные.НазваниеПапки);
					ОбщегоНазначения.СкопироватьПереместитьКаталог(ПолныйПутьКПапкеИсточника, ПолныйПутьКЦелевомуКаталогу, Истина);
					
					// Отправим сообщение что закончили
					Сообщение = Новый Структура();
					Сообщение.Вставить("ТелеграмКонтакт", СсылкаДокВхСообщение.ТелеграмКонтакт);
					Сообщение.Вставить("Текст", "Операция завершена");
					ТелеграмМодуль.СоздатьИОтправитьДокСообщениеИсходящие(Сообщение);
					
					ТекущиеДанные.Вставить("ОжидаемыйТипОтветаПользователя", "ОперацияЗавершена");
				КонецЕсли;
			
			КонецЕсли;
				
			МенеджерЗаписи.ТекущиеДанные = Новый ХранилищеЗначения(ТекущиеДанные);
			МенеджерЗаписи.ДатаЗаписи = ТекущаяДата();
			МенеджерЗаписи.Записать(Истина);
		Иначе
			ЗаписьВЖурналОшибок("Обработки.ТелеграмИИОбработкаСообщенийГруппы", "МодульОбъекта", "Обработка пустого сообщения. Сообщение входящие=""" + СсылкаДокВхСообщение + """");	
		КонецЕсли;
	КонецЕсли;
	
	
	
КонецПроцедуры
