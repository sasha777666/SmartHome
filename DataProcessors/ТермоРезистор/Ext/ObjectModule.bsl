
//Терморезистор должен быть подключён к ВЫСОКОМУ напряжению и аналоговому пину,
//подтягивающий резистор должен быть подключён к ЗЕМЛЕ и аналоговому пину
Процедура ПроцессДляСервера(Процесс = "") Экспорт
	
	ИнициирующийПроцесс = Процесс;
	
	Инициализация();	
	ПолучитьДанныеСДатчика();
	 
	Если ДатаПолученияДанных <> '00010101' Тогда 
		ЗаписатьТекущееДанные();	
	КонецЕсли;

КонецПроцедуры

//////
Процедура Инициализация()
	
	Ответ = ЗаполнитьПины();
	Если ЗначениеЗаполнено(Ответ) Тогда 
		Сообщения = Сообщения + Символы.ПС + ТекущаяДата() + " " + Ответ;
	КонецЕсли;
	
	Ответ = ЗаполнитьНастройки();
	Если ЗначениеЗаполнено(Ответ) Тогда 
		Сообщения = Сообщения + Символы.ПС + ТекущаяДата() + " " + Ответ;
	КонецЕсли;

	
КонецПроцедуры
Функция ЗаполнитьПины() 
	
	СообщениеСНенайденнымиПинами = "";
	
	СтрокаСНастройкой = Оборудование.Пины.Найти("Терморезистор", "Наименование");
	Если СтрокаСНастройкой = Неопределено Тогда 
		СообщениеСНенайденнымиПинами = СообщениеСНенайденнымиПинами + "Не задан ПИН для ""Терморезистор""; ";
	Иначе
		НомерПина = СтрокаСНастройкой.НомерПина;
	КонецЕсли;
	
	Возврат СообщениеСНенайденнымиПинами;
	
КонецФункции
Функция  ЗаполнитьНастройки() 
	
	СообщениеСНенайденнымиНастройками = "";
	
	// Настройки в реквизитах
	СтрокаСТабСНастройкамиРеквизитов = Оборудование.Настройки.Найти("Реквизиты",  "Наименование");
	Если СтрокаСТабСНастройкамиРеквизитов = Неопределено Тогда 
		СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не заполнена настройка для реквизитов ""Реквизиты"" (В справочнике оборудование); ";
	Иначе
		ТабСНастройкамиРеквизитов = СтрокаСТабСНастройкамиРеквизитов.Настройка.ТаблицаНастроек;
				
		СтрокаСПараметром = ТабСНастройкамиРеквизитов.Найти("СопротивлениеНаЗемлюОМ", "Параметр");
		Если СтрокаСПараметром = Неопределено Тогда 
			СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не указан параметр ""СопротивлениеНаЗемлюОМ""; ";
		Иначе
			СопротивлениеНаЗемлю = СтрокаСПараметром.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
	// Настройки СоответствиеСопротивленияТемпературы
	СтрокаСТабСоответствиеСопротивленияТемпературы = Оборудование.Настройки.Найти("СоответствиеСопротивленияТемпературы",  "Наименование");
	Если СтрокаСТабСоответствиеСопротивленияТемпературы = Неопределено Тогда 
		СообщениеСНенайденнымиНастройками = СообщениеСНенайденнымиНастройками + "Не заполнена настройка ""СоответствиеСопротивленияТемпературы"" (В справочнике оборудование); ";
	Иначе
		ТабСоответствиеСопротивленияТемпературы = СтрокаСТабСоответствиеСопротивленияТемпературы.Настройка.ТаблицаНастроек;
		СоответствиеСопротивленияТемпературы.Очистить();
		Для Каждого Строка Из ТабСоответствиеСопротивленияТемпературы Цикл
			нСтрока = СоответствиеСопротивленияТемпературы.Добавить();	
			нСтрока.Сопротивление = Строка.Параметр;
			нСтрока.Температура = Строка.Значение;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат СообщениеСНенайденнымиНастройками;

КонецФункции


Процедура ПолучитьДанныеСДатчика()
	
	Ответ = К._analogRead_СреднееАрифметическое(Оборудование, НомерПина, 1);

	Если ТипЗнч(Ответ) = Тип("Число") Тогда 
		Сопротивление = ОпределитьСопротивление(Ответ, СопротивлениеНаЗемлю);	
		Если Сопротивление = 99999999999999999999999999999999 Тогда // Ошибка, возможно разрыв провода
			Сообщения = Сообщения + "Сбой датчика температуры 1 " + ТекущаяДата() + "; "; 
		КонецЕсли;
		ТекущаяТемпература = ОпределитьТемпературуПоСопротивлению(Сопротивление);	
		ДатаПолученияДанных = ТекущаяДата();
	Иначе
		Сообщения = Сообщения + Символы.ПС + ТекущаяДата() + "Не удалось получить данные температуры и влажности";
	КонецЕсли;	
	
КонецПроцедуры

// Сопротивление - Пересчитывает полученные значение с ардуины в сопротивление. 
// Возвращает: Сопротивление в Омах (кОмах, мОмах) 
// Параметры:
// ЗначениеСАрдуины - Число - Значение полученное Аналоговым пином
// СопрРезистораНаЗемлю - Число - Сопротивление резистора соеденяющего пин с землёй (в Омах, кОмах, мОмах)
Функция ОпределитьСопротивление(ЗначениеСАрдуины, СопрРезистораНаЗемлю) Экспорт
	
	Если ЗначениеСАрдуины = 0 Тогда // Сопротивление на питание много больше чем на землю 
	Сопротивление = 99999999999999999999999999999999;
	Иначе
		Сопротивление = 1023 / ЗначениеСАрдуины	* СопрРезистораНаЗемлю - СопрРезистораНаЗемлю;
	КонецЕсли;
	
	Возврат Сопротивление;	
	
КонецФункции       
Функция ОпределитьТемпературуПоСопротивлению(Сопротивление)
	
	СоответствиеСопротивленияТемпературы.Сортировать("Сопротивление");
	
	ВсегоСтрок = СоответствиеСопротивленияТемпературы.Количество();

	НижГраница = 1;
	ВерхГрачница = ВсегоСтрок; 
	
	Пока ВерхГрачница - НижГраница > 1 Цикл	
		ТекСтрока = Окр((ВерхГрачница + НижГраница)/2);
		Если Число(СоответствиеСопротивленияТемпературы[ТекСтрока-1].Сопротивление) > Сопротивление Тогда 
			ВерхГрачница = ТекСтрока;
		Иначе
			НижГраница = ТекСтрока;
		КонецЕсли;		
	КонецЦикла;
	
	Если  Число(СоответствиеСопротивленияТемпературы[ВерхГрачница-1].Сопротивление) - Сопротивление <  
	Сопротивление - Число(СоответствиеСопротивленияТемпературы[НижГраница-1].Сопротивление) Тогда // температура ближе к верхней отметке
		Темература = СоответствиеСопротивленияТемпературы[ВерхГрачница-1].Температура;
	Иначе // температура ближе к нижней отметке
		Темература = СоответствиеСопротивленияТемпературы[НижГраница-1].Температура;
	КонецЕсли;
 
	
	Возврат Темература;
	
КонецФункции

/////	

Процедура ЗаписатьТекущееДанные()
	
	ОбщегоНазначения.ЗаписатьДанныеОборудования(Оборудование, "Температура", ТекущаяТемпература, ДатаПолученияДанных); 
	
КонецПроцедуры

